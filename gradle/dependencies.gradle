def groovyVer = "2.5.8"
def spockGroovyVer = groovyVer.replaceAll(/\.\d+$/, '')

ext {
  versions = [
    opentelemetry: '0.1.0-SNAPSHOT',

    slf4j      : "1.7.28",
    guava      : "20.0", // Last version to support Java 7

    jackson    : "2.9.9.3", // https://nvd.nist.gov/vuln/detail/CVE-2019-14379

    spock      : "1.3-groovy-$spockGroovyVer",
    groovy     : groovyVer,
    junit      : "4.12",
    logback    : "1.2.3",
    lombok     : "1.18.8",
    bytebuddy  : "1.10.0",
    scala      : "2.11.12",  // Last version to support Java 7 (2.12+ require Java 8+)
    kotlin     : "1.3.50",
    coroutines : "1.3.0"
  ]

  deps = [
    // OpenTelemetry
    opentelemetryApi : dependencies.create(group: 'io.opentelemetry', name: 'opentelemetry-api', version: versions.opentelemetry),
    opentelemetry    : [
      dependencies.create(group: 'io.opentelemetry', name: 'opentelemetry-api', version: versions.opentelemetry),
      dependencies.create(group: 'io.opentelemetry', name: 'opentelemetry-sdk', version: versions.opentelemetry),
    ],

    // General
    slf4j          : "org.slf4j:slf4j-api:${versions.slf4j}",
    guava          : "com.google.guava:guava:$versions.guava",
    jackson        : [
      dependencies.create(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson),
      dependencies.create(group: 'org.msgpack', name: 'jackson-dataformat-msgpack', version: '0.8.17'),
    ],
    bytebuddy      : dependencies.create(group: 'net.bytebuddy', name: 'byte-buddy', version: "${versions.bytebuddy}"),
    autoservice    : [
      dependencies.create(group: 'com.google.auto.service', name: 'auto-service', version: '1.0-rc3'),
      dependencies.create(group: 'com.google.auto', name: 'auto-common', version: '0.8'),
      // These are the last versions that support guava 20.0.  Upgrading has odd interactions with shadow.
      dependencies.create(group: 'com.google.guava', name: 'guava', version: "${versions.guava}"),
    ],

    // Testing
    spock          : dependencies.create("org.spockframework:spock-core:${versions.spock}", {
      exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }),
    bytebuddyagent : dependencies.create(group: 'net.bytebuddy', name: 'byte-buddy-agent', version: "${versions.bytebuddy}"),
    groovy         : "org.codehaus.groovy:groovy-all:${versions.groovy}",
    junit          : "junit:junit:${versions.junit}",
    testcontainers : "org.testcontainers:testcontainers:1.7.3",
    testLogging    : [
      dependencies.create(group: 'ch.qos.logback', name: 'logback-classic', version: versions.logback),
      dependencies.create(group: 'org.slf4j', name: 'log4j-over-slf4j', version: versions.slf4j),
      dependencies.create(group: 'org.slf4j', name: 'jcl-over-slf4j', version: versions.slf4j),
      dependencies.create(group: 'org.slf4j', name: 'jul-to-slf4j', version: versions.slf4j),
    ],
    scala          : dependencies.create(group: 'org.scala-lang', name: 'scala-library', version: "${versions.scala}"),
    kotlin         : dependencies.create(group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "${versions.kotlin}"),
    coroutines     : dependencies.create(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: "${versions.coroutines}"),
  ]
}
